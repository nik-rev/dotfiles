#!/usr/bin/env nu

const c = $nu.default-config-dir | path dirname

const LINUX = $nu.os-info.name == "linux"
const WINDOWS = $nu.os-info.name == "windows"

const zed = if $WINDOWS { "Zed" } else { "zed" }
let firefox_profile = match $nu.os-info.name {
    "linux" => $"($nu.home-path)/.mozilla/firefox"
    "macos" => $"($c)/Firefox/Profiles"
    "windows" => $"($c)/Mozilla/Firefox/Profiles"
}
let firefox_profile = if ($firefox_profile | path exists) { ls $firefox_profile | where { $in.name | str ends-with ".default-release" } | first | get name }
let firefox_policies = match $nu.os-info.family {
    "windows" => "~/scoop/apps/firefox/current"
    "unix" => "/etc/firefox/policies"
}

const SUDO = false

let paths = {
    alacritty.toml: $"($c)/alacritty/alacritty.toml"
    cargo.config.toml: ~/.cargo/config.toml
    git.config: $"($c)/git/config"
    gitui.theme.ron: $"($c)/gitui/theme.ron"
    glazewm.yaml: (if $WINDOWS { "~/.glzr/glazewm/config.yaml" })
    kitty.conf: $"($c)/kitty/kitty.conf"
    lazygit.yml: $"($c)/lazygit/config.yml"
    rio.toml: $"($c)/rio/config.toml"
    sway.config: $"($c)/sway/config"
    wezterm.lua: $"($c)/wezterm/wezterm.lua"
    firefox.user.js: $"($firefox_profile)/user.js"
    firefox.policies.json: (if $SUDO { $"($firefox_policies)/policies.json" })
    yazi.toml: $"($c)/yazi/yazi.toml"
    yazi.theme.toml: $"($c)/yazi/theme.toml"
    zed.settings.jsonc: $"($c)/($zed)/settings.json"
    zed.keymap.jsonc: $"($c)/($zed)/keymap.json"
    autologin.conf: (if $SUDO and $LINUX { "/etc/systemd/system/getty@tty1.service.d/autologin.conf" })
    bat: $"($c)/bat"
    nushell: $"($c)/nushell"
    presenterm: $"($c)/presenterm"
    helix: $"($c)/helix"
    bash.profile: (if $LINUX { "~/.bash_profile" })
    niri.config.kdl: (if $LINUX { $"($c)/niri/config.kdl" })
    xdg-desktop-portal-termfilechooser.config: (if $LINUX { $"($c)/xdg-desktop-portal-termfilechooser/config" })
}

$paths
| compact
| items { |src, dest|
    let src = $"($env.FILE_PWD)/($src)" | path join | path expand
    let dest = $dest | path expand
    [$src $dest]
}
| par-each { |paths|
    let src = $paths.0
    let dest = $paths.1
    mkdir ($dest | path dirname)
    rm -rf $dest
    cp -r $src $dest
}
| ignore
