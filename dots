#!/usr/bin/env nu

# Directory where all config files are. This is available via "#", analogous for "~" for the home directory
const c = $nu.default-config-dir | path dirname

const zed = if $nu.os-info.name == "windows" { "Zed" } else { "zed" }
let firefox_profile = match $nu.os-info.name {
    "linux" => "~/.mozilla/firefox"
    "macos" => $"($c)/Firefox/Profiles"
    "windows" => $"($c)/Mozilla/Firefox/Profiles"
}
let firefox_profile = ls $firefox_profile | first | get name
let firefox_policies = match $nu.os-info.family {
    "windows" => "~/scoop/apps/firefox/current"
    "unix" => "/etc/firefox/policies"
}
let glazewm_path = if $nu.os-info.name == "windows" { "~/.glzr/glazewm/config.yaml" }

let paths = {
    alacritty.toml: $"($c)/alacritty/alacritty.toml"
    cargo.config.toml: ~/.cargo/config.toml
    git.config: $"($c)/git/config"
    gitui.theme.ron: $"($c)/gitui/theme.ron"
    glazewm.yaml: $glazewm_path
    kitty.conf: $"($c)/kitty/kitty.conf"
    lazygit.yml: $"($c)/lazygit/config.yml"
    rio.toml: $"($c)/rio/config.toml"
    sway.config: $"($c)/sway/config"
    wezterm.lua: $"($c)/wezterm/wezterm.lua"
    firefox.user.js: $"($firefox_profile)/user.js"
    firefox.policies.json: $"($firefox_policies)/policies.json"
    yazi.toml: $"($c)/yazi/yazi.toml"
    yazi.theme.toml: $"($c)/yazi/theme.toml"
    zed.settings.jsonc: $"($c)/($zed)/settings.json"
    zed.keymap.jsonc: $"($c)/($zed)/keymap.json"
    bat: $"($c)/bat"
    nushell: $"($c)/nushell"
    presenterm: $"($c)/presenterm"
    helix: $"($c)/helix"
}

$paths
| items { |src, dest|
    let src = $"($env.FILE_PWD)/($src)" | path join | path expand
    let dest = $dest | path expand
    [$src $dest]
}
| par-each { |paths|
    let src = $paths.0
    let dest = $paths.1
    if $dest == null {
        return
    }
    mkdir ($dest | path dirname)
    cp -r $src $dest
}
| ignore
